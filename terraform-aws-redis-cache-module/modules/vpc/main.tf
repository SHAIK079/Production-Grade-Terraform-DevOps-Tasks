resource "aws_vpc" "main" {

  cidr_block = var.vpc_cidr


  enable_dns_support = true

  enable_dns_hostnames = true


  tags = {

    Name = "${var.name}-vpc"

  }

}



# Internet Gateway

resource "aws_internet_gateway" "igw" {


  vpc_id = aws_vpc.main.id


  tags = {

    Name = "${var.name}-igw"

  }

}



# Public Subnets

resource "aws_subnet" "public" {


  count = length(var.public_subnet_cidrs)


  vpc_id = aws_vpc.main.id


  cidr_block = var.public_subnet_cidrs[count.index]


  availability_zone = var.azs[count.index]


  map_public_ip_on_launch = true



  tags = {

    Name = "${var.name}-public-${count.index + 1}"

  }

}



# Private Subnets

resource "aws_subnet" "private" {


  count = length(var.private_subnet_cidrs)


  vpc_id = aws_vpc.main.id


  cidr_block = var.private_subnet_cidrs[count.index]


  availability_zone = var.azs[count.index]



  tags = {

    Name = "${var.name}-private-${count.index + 1}"

  }

}




# Public Route Table

resource "aws_route_table" "public" {


  vpc_id = aws_vpc.main.id


  route {

    cidr_block = "0.0.0.0/0"


    gateway_id = aws_internet_gateway.igw.id

  }


  tags = {

    Name = "${var.name}-public-route"

  }

}




# Public Route Association

resource "aws_route_table_association" "public" {


  count = length(var.public_subnet_cidrs)


  subnet_id = aws_subnet.public[count.index].id


  route_table_id = aws_route_table.public.id

}



# NAT Elastic IP

resource "aws_eip" "nat" {


  domain = "vpc"

}



# NAT Gateway

resource "aws_nat_gateway" "nat" {


  allocation_id = aws_eip.nat.id


  subnet_id = aws_subnet.public[0].id



  tags = {

    Name = "${var.name}-nat"

  }

}




# Private Route Table

resource "aws_route_table" "private" {


  vpc_id = aws_vpc.main.id


  route {


    cidr_block = "0.0.0.0/0"


    nat_gateway_id = aws_nat_gateway.nat.id

  }


  tags = {

    Name = "${var.name}-private-route"

  }

}




# Private Route Association

resource "aws_route_table_association" "private" {


  count = length(var.private_subnet_cidrs)


  subnet_id = aws_subnet.private[count.index].id


  route_table_id = aws_route_table.private.id


}